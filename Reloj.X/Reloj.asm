;	    RELOJ DE PRECISIÓN
; 
; AUTOR: RODRÍGUEZ RAMÍREZ LUIS EDUARDO
;	    
;	    EL OBJETIVO DE ESTE PROYECTO ES TENER UN RELOJ PRECISO
;QUE SERÁ MOSTRADO A TRAVÉS DE UN DISPLAY DE 7 SEGMENTOS Y 4 DIGITOS POR MEDIO
;DEL PUERTO B Y PARTE DEL D
	    
	    LIST P=16F887
	    INCLUDE <P16F887.INC>

	    __CONFIG    _CONFIG1, _LVP_OFF & _FCMEN_ON & _IESO_OFF & _BOR_OFF & _CPD_OFF & _CP_OFF & _MCLRE_ON & _PWRTE_ON & _WDT_OFF & _INTRC_OSC_NOCLKOUT
	    __CONFIG    _CONFIG2, _WRT_OFF & _BOR21V
	    
;	    INICIALIZACIÓN DEL MICROCONTROLADOR
;	    
;DEFINICIÓN DE ESPACIOS DE MEMORIA, ENTRADAS Y SALIDAS, DESACTIVACIÓN DE ENTRADAS ANALÓGICAS
	    
	    ORG		0x00
RET 	    EQU		0X20
UNI 	    EQU		0X21
DEC 	    EQU		0X22
CEN 	    EQU		0X23
MIL 	    EQU		0X24
CUENTA	    EQU		0x25	    
	    BSF		STATUS, RP0
	    BCF		STATUS, RP1		;BANCO 1
	    CLRF	TRISA
	    CLRF	TRISB
	    CLRF	TRISC
	    CLRF	TRISD
	    CLRF	TRISE			;TODAS COMO SALIDAS
	    
	    MOVLW	B'11010101'		;PREESCALADOR 1:64
	    MOVWF	OPTION_REG		;TIMER 0 COMO TEMPORIZADOR
	    
	    BSF		STATUS, RP0
	    BSF		STATUS, RP1		;BANCO 3
	    CLRF	ANSEL			;											    QUE ME VEZ PERRO XD
	    CLRF	ANSELH			;DESACTIVAR TODAS LAS ENTRADAS ANALOGICAS
	    
	    BCF		STATUS, RP0
	    BCF		STATUS, RP1		;BANCO 0
;-----------------------------------------------
;---------------CICLO PRINCIPAL-----------------
;-----------------------------------------------
	    CLRF	PORTB
	    CLRF	UNI
	    CLRF	DEC
	    CLRF	CEN
	    CLRF	MIL
CICLO	    ;	INCREMENTAR UNIDADES    
	    CALL	RETARDO			;Espera
	    INCF	UNI, F			;INCREMENTA LAS UNIDADES
	    MOVLW	0x0A			;GUARDA 10 EN W
	    SUBWF	UNI, W			;COMPARA LAS UNIDADES CON 10
	    BTFSS	STATUS, Z		;REVISA SI FUE CERO
	    GOTO	CICLO
;	INCREMENTAR DECENAS	
	    CLRF	UNI			;REINICIA LAS UNIDADES
	    INCF	DEC, F			;INCREMENTA LAS DECENAS
	    MOVLW	0x06			;GUARDA 6 EN W
	    SUBWF	DEC, W			;COMPARA LAS DECENAS CON 6
	    BTFSS	STATUS, Z		;REVISA SI FUE CERO
	    GOTO	CICLO
	    
;	INCREMENTA LAS CENTENAS
	    CLRF	DEC			;REINICIA LAS DECENAS
	    INCF	CEN, F			;INCREMENTA LAS CENTENAS
	    MOVLW	0x0A			;GUARDA 10 EN W
	    SUBWF	CEN, W			;COMPARA CENTENAS CON 10
	    BTFSS	STATUS, Z		;REVISA SI FUE CERO
	    GOTO	CICLO			
	    
;	INCREMENTA LOS MILLARES
	    CLRF	CEN			;REINICIA LAS CENTENAS
	    INCF	MIL, F			;INCREMENTA LOS MILLARES
	    MOVLW	0x06			;GUARDA 6 EN W
	    SUBWF	MIL, W			;COMPARA MILLARES CON 6
	    BTFSS	STATUS, Z		;REVISA SI FUE CERO
	    GOTO	CICLO			
	    CLRF	MIL			;REINICIA LOS MILLARES
	    GOTO	CICLO
	    
;-----------------------------------------------
;---------------------RETARDO-------------------
;-----------------------------------------------	    
	    
RETARDO	    MOVLW	0x3D;3D			;VALOR PARA EL RETARDO DEFINIDO EXPERIMENTALMENTE
	    MOVWF	RET			;GUARDAR EL VALOR EN EL RETARDO
	    
	    CALL	BARRIDO			;HACE UN BARRIDO DE ANODOS
	    BTFSS	INTCON, 2		;REVISA SI HAY DESBORDAMIENTO
	    GOTO	$-2			;REGRESA 2 INSTRUCCIONES
	    
	    BCF		INTCON, 2		;REINICIA EL DESBORDAMIENTO
	    DECFSZ	RET			;DECREMENTA EL RETARDO
	    GOTO	$-5			;REGRESA HASTA EL LLAMADO DEL BARRIDO (5 INSTRUCCIONES)
	    RETURN
	    
;-----------------------------------------------
;--------------BARRIDO DE ANODOS----------------
;-----------------------------------------------	    
	    
BARRIDO	    CALL	RET2			;ESPERA UN MOMENTO
	    MOVF	UNI, W			;GUARDA UNIDADES EN W
	    CALL	DISPLAY			;Y ASIGNA UN NUMERO
	    MOVWF	PORTB			;LO MUESTRA EN EL PUERTO B
	    MOVLW	B'10000000'		;Y LO MUESTRA EN UN DIGITO
	    MOVWF	PORTD			;POR EL PUERTO B
	    
	    CALL	RET2			;LO MISMO QUE ARRIBA
	    MOVF	DEC, W
	    CALL	DISPLAY
	    MOVWF	PORTB
	    MOVLW	B'01000000'
	    MOVWF	PORTD
	    
	    CALL	RET2			;LO MISMO QUE ARRIBA
	    MOVF	CEN, W
	    CALL	DISPLAY
	    MOVWF	PORTB
	    MOVLW	B'00100000'
	    MOVWF	PORTD
	    
	    CALL	RET2			;LO MISMO QUE ARRIBA
	    MOVF	MIL, W
	    CALL	DISPLAY
	    MOVWF	PORTB
	    MOVLW	B'00010000'
	    MOVWF	PORTD
	    
	    RETURN
	    
;-----------------------------------------------
;------------MOSTRAR DATOS EN DISPLAYS----------
;-----------------------------------------------	    

DISPLAY	    ADDWF	PCL, F			;MODIFICA EL CONTADOR DE INSTRUCCIONES
	    RETLW	0xFC			;NUMERO 0
	    RETLW	0x60			;NUMERO 1
	    RETLW	0xDA			;NUMERO 2
	    RETLW	0xF2			;NUMERO 3
	    RETLW	0x66			;NUMERO 4
	    RETLW	0xB6			;NUMERO 5
	    RETLW	0xBE			;NUMERO 6
	    RETLW	0xE0			;NUMERO 7
	    RETLW	0xFE			;NUMERO 8
	    RETLW	0xE6			;NUMERO 9	    
	    
;-----------------------------------------------
;------------------RETARDO 2--------------------
;----------PARA EL BARRIDO DE ANODOS------------
RET2	    MOVLW	0xFF			;CARGA FF EN W
	    MOVWF	CUENTA			;Y LO MUEVE EN CUENTA
	    DECFSZ	CUENTA, F		;RESTA 1 A CUENTA
	    GOTO	$-1			;REGRESA 1 INSTRUCCION
	    RETURN
	    
	    END